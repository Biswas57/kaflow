plugins {
    id 'java'
    id 'jacoco'
    id("org.barfuin.gradle.jacocolog") version "3.1.0"
    id 'com.google.protobuf' version '0.9.4'
    id 'org.springframework.boot' version '2.7.17'
    id 'io.spring.dependency-management' version '1.1.4'
}

repositories {
    // Use Maven Central for resolving dependencies.
    mavenCentral()
}

ext {
    grpcVersion = '1.63.0'
    protobufVersion = '3.25.3'
}

dependencies {
    /* gRPC runtime */
    implementation "io.grpc:grpc-netty-shaded:${grpcVersion}"
    implementation "io.grpc:grpc-protobuf:${grpcVersion}"
    implementation "io.grpc:grpc-stub:${grpcVersion}"

    /* Spring-Boot integration for gRPC */
    implementation 'net.devh:grpc-server-spring-boot-starter:2.15.0.RELEASE'

    /* JSON helper (can be removed once you return POJOs everywhere) */
    implementation 'org.json:json:20230618'

    /* Spring Web & Actuator */
    implementation 'org.springframework.boot:spring-boot-starter-web'
    implementation 'org.springframework.boot:spring-boot-starter-actuator'

    /* test */
    testImplementation 'com.github.stefanbirkner:system-lambda:1.0.0'
    testImplementation 'org.mockito:mockito-core:4.5.1'
    testImplementation 'org.junit.jupiter:junit-jupiter:5.9.3'
    testRuntimeOnly 'org.junit.platform:junit-platform-launcher'
    testImplementation 'org.springframework.boot:spring-boot-starter-test'
}

sourceSets {
    main {
        java {
            srcDir 'src/main/java'
        }
        resources {
            srcDir 'src/main/resources'
        }
    }
    test {
        java {
            srcDir 'src/test/java'
        }
        resources {
            srcDir 'src/test/resources'
        }
    }
}

processResources {
    duplicatesStrategy = 'INCLUDE'
}

tasks.named('test') {
    // Use JUnit Platform for unit tests.
    useJUnitPlatform()
}

// Apply a specific Java toolchain to ease working on different environments.
java {
    toolchain { languageVersion = JavaLanguageVersion.of(17) }
    modularity.inferModulePath = true
}

jacocoTestReport {
    reports {
        xml.required = true
        csv.required = false
    }

    afterEvaluate {
        classDirectories.from = files(classDirectories.files.collect {
            fileTree(dir: it, exclude: [
               "tributary/cli/**/*"
            ])
        })
    }

    dependsOn test // tests are required to run before generating the report
}

jacocoTestCoverageVerification {
    violationRules {
        rule {
            limit {
                counter = "BRANCH"
                minimum = 0.80
            }
        }
    }

    afterEvaluate {
        classDirectories.from = files(classDirectories.files.collect {
            fileTree(dir: it, exclude: [
                "tributary/cli/**/*"
            ])
        })
    }

    dependsOn jacocoTestReport
}

protobuf {
    protoc { artifact = "com.google.protobuf:protoc:3.25.3" }
    plugins {
        grpc { artifact = "io.grpc:protoc-gen-grpc-java:1.63.0" }
    }
    generateProtoTasks {
        all().each { task ->
            task.plugins { grpc {} }
        }
    }
}

task coverage {
    description("Generate coverage report and check if above threshold if it exists")
    group("verification")
    dependsOn('jacocoTestCoverageVerification')
}

jacocoLogTestCoverage {
    logAlways = true
    counters {
        showClassCoverage = false
        showMethodCoverage = false
        showInstructionCoverage = false
        showComplexityCoverage = false
    }
}